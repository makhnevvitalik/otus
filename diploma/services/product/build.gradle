import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.google.cloud.tools.jib' version '2.4.0'
    id "com.google.cloud.tools.minikube" version "1.0.0-alpha.3"
    id "io.freefair.lombok" version "5.0.0-rc6"
    id 'java'
}

group = 'com.otus'
version = '1.0.0'
sourceCompatibility = '11'

lombok {
    config['lombok.anyconstructor.addconstructorproperties'] = 'true'
    config['lombok.addLombokGeneratedAnnotation'] = 'true'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR6')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.4.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.3.2'
    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation 'com.github.rutledgepaulv:q-builders:1.6'
    implementation 'com.github.rutledgepaulv:rest-query-engine:0.7.1'

    def mapStructVersion = '1.3.1.Final'
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
}

minikubeStart {
    flags = ["--vm-driver=virtualbox"]
}

jib {
    from {
        image = 'bellsoft/liberica-openjdk-alpine:11'
    }
    to {
        image = "makhnevvitalik/otus-product"
        auth {
            username = System.getenv('CC_DOCKER_USERNAME')
            password = System.getenv('CC_DOCKER_PASSWORD')
        }
        tags = ['1.0.3']
    }
}

tasks['jib'].doFirst {
    jib {
        dockerClient {
            environment = minikube.getDockerEnv('minikube')
        }
    }
}

test {
    useJUnitPlatform()
}
